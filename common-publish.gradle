apply plugin : 'maven-publish'

tasks.register('androidJavadoc', Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }

    exclude '**/R.html', '**/R.*.html', '**/index.html'
    options.encoding 'utf-8'
    options {
        addStringOption 'docencoding', 'utf-8'
        addStringOption 'charset', 'utf-8'
        links 'https://docs.oracle.com/javase/7/docs/api/'
        links 'https://d.android.com/reference'
        links 'https://developer.android.com/reference/androidx/'
    }
}

tasks.register('androidJavadocJar', Jar) {
    dependsOn dokkaJavadoc
    archiveClassifier.set('javadoc')
    from androidJavadoc.destinationDir
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.register('javaSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = "verify${project.path.replace(":","-")}"
                groupId = "com.github.ibm-verify"
                version = "$versionName"
                pom {
                    name = artifactId
                    description = "IBM Verify ${project.name.toUpperCase()} SDK for Android"
                    url = 'https://github.com/ibm-verify/verify-sdk-android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/ibm-verify/verify-sdk-android/blob/main/LICENSE'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/ibm-verify/verify-sdk-android.git'
                        developerConnection = 'scm:git:ssh://github.com/ibm-verify/verify-sdk-android.git'
                        url = 'https://github.com/ibm-verify/verify-sdk-android'
                    }
                    developers {
                        developer {
                            id = 'ibm-verify'
                            name = 'IBM Verify'
                            email = 'verify@au1.ibm.com'
                        }
                    }
                }
            }
        }
    }
}